buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE'
        classpath 'com.moowork.gradle:gradle-gulp-plugin:0.11'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.11'
    }
}

apply plugin: 'spring-boot'
apply plugin: 'groovy'
apply plugin: 'com.moowork.gulp'
apply plugin: 'com.moowork.node'
apply from: "${rootDir}/gradle/integrationTests.gradle"

springBoot {
    mainClass = 'greenmoonsoftware.tidewater.web.Application'
}

gulp {
    workDir = file("${project.projectDir}/src/main/ng")
}

node {
    version = '5.0.0'
    download = true
}

configurations {
    plugin {
        transitive = false
    }
}

dependencies {
    compile project(':runtime')

    compile 'com.h2database:h2:1.4.186'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'org.codehaus.groovy:groovy:2.4.4'
    compile 'org.codehaus.groovy:groovy-json:2.4.4'
    compile 'org.flywaydb:flyway-core'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-websocket'
    compile 'org.springframework:spring-messaging'

    //Add all known plugins
    project(':plugins').childProjects.each {
        plugin project(path: it.value.path, configuration: 'plugin')
    }

    testCompile 'org.springframework:spring-test'
    testCompile 'org.testng:testng:6.9.6'
}

task copyPlugins(type: Copy) {
    from configurations.plugin
    into "${project.buildDir}/plugins"
}

test {
    useTestNG()
    jvmArgs = ['-noverify']
}

processResources.dependsOn gulp_build